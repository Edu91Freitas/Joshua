apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'

task debugCoverageReport(type: JacocoReport, dependsOn: ['testDebugUnitTest', 'connectedDebugAndroidTest']) {
    def kotlinClasses = fileTree(dir: "$buildDir/tmp/kotlin-classes/debug")

    def coverageSourceDirs = ['src/debug/kotlin', 'src/main/kotlin']

    def executionDataDirs = fileTree(dir: "$buildDir", includes: [
            'jacoco/testDebugUnitTest.exec',
            'outputs/code_coverage/debugAndroidTest/connected/*.ec'
    ])

    classDirectories.setFrom(files([kotlinClasses]))
    additionalSourceDirs.setFrom(files(coverageSourceDirs))
    sourceDirectories.setFrom(coverageSourceDirs)
    executionData.setFrom(executionDataDirs)

    reports {
        xml.enabled = true
        html.enabled = true
    }
}

coveralls {
    jacocoReportPath "$buildDir/reports/jacoco/debugCoverageReport/debugCoverageReport.xml"
}
